
/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array binaryArray;

    /** Initializes the library. */
    function void init() {
        let binaryArray = Array.new(16);
        let binaryArray[0] = 1;
        let binaryArray[1] = 2;
        let binaryArray[2] = 4;
        let binaryArray[3] = 8;
        let binaryArray[4] = 16;
        let binaryArray[5] = 32;
        let binaryArray[6] = 64;
        let binaryArray[7] = 128;
        let binaryArray[8] = 256;
        let binaryArray[9] = 512;
        let binaryArray[10] = 1024;
        let binaryArray[11] = 2048;
        let binaryArray[12] = 4096;
        let binaryArray[13] = 8192;
        let binaryArray[14] = 16384;
        let binaryArray[15] = 16384 + 16384;
        return;
    }

    /** Returns if jth bit of x is 1 */
    function boolean bit(int x, int j){
        return ~((x & binaryArray[j]) = 0);
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if(x < 0){
            let x = -x;
        }
        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX,j;

        let sum = 0;
        let shiftedX = x;
        let j = 0;

        while(j < 16){
            if(Math.bit(y,j)){
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let j = j + 1;
        }

        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q,result;
        var boolean pos;

        let pos = ((x < 0) = (y < 0));

        let x = Math.abs(x);
        let y = Math.abs(y);

        if(y > x){
            return 0;
        }

        let q = Math.divide(x,y + y);
        
        if((x - (2 * q * y)) < y){
            let result = q + q;
        }else{
            let result = q + q + 1;
        }

        if(pos){
            return result;
        }else{
            return -result;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y,j,temp,tempQ;

        let y = 0;
        let j = 7; // j = n/2 -1;

        while(~(j < 0)){
            let temp = y + binaryArray[j];
            let tempQ = temp * temp;
            //avoid overflow
            if(~(tempQ > x) & (tempQ > 0)){
                let y = temp;
            }
            let j = j - 1;
        }

        return y;
    }

    /** helper function two to the*/
    function int two_To_The(int i){
        return binaryArray[i];
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if(a > b){
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if(a > b){
            return b;
        }
        return a;
    }
}
